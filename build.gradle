plugins {
    id 'com.verificationgentleman.gradle.hdvl.systemverilog' version '0.1.0-SNAPSHOT'
}

group 'com.verificationgentleman'

sourceSets {
    main {
        svHeaders {
            srcDirs = ['sv']
        }
    }
}

extensions.xrunDir = file("$buildDir/xrun")

task runExample(type: RunExampleTask) {
    dependsOn genFullArgsFile
}

class RunExampleTask extends DefaultTask {
    @Option(option = 'name', description = 'name of the example to run')
    String exampleName

    @OptionValues('name')
    def List<String> getAvailableExamples() {
	def exampleNames = []
	project.file('examples').eachDir {
	    exampleNames << it.name
        }
	return exampleNames
    }

    @TaskAction
    def run() {
        checkExampleName()
        project.exec {
            executable 'xrun'
            args'-f', project.genFullArgsFile.destination.get().asFile
            args exampleFiles
            workingDir project.xrunDir
            project.mkdir workingDir
        }
    }

    def checkExampleName() {
        def isLegal = exampleName in getAvailableExamples()
        if (!isLegal)
            throw new InvalidUserDataException('illegal example name')
    }

    def getExampleFiles() {
        def dirPath = project.file("examples/$exampleName").path
        def files = new FileNameFinder().getFileNames dirPath,'*.sv'
        return files
    }
}
