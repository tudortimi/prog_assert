plugins {
    id 'com.verificationgentleman.gradle.hdvl.systemverilog' version '0.1.0-SNAPSHOT'
}

group 'com.verificationgentleman'

sourceSets {
    main {
        svHeaders {
            srcDirs = ['sv']
        }
    }
}

extensions.xrunDir = file("$buildDir/xrun")

task runExample(type: RunExampleTask) {
    dependsOn genFullArgsFile
}

class RunExampleTask extends DefaultTask {
    @Option(option = 'name', description = 'name of the example to run')
    String exampleName

    @TaskAction
    void run() {
        project.exec {
            executable 'xrun'
            args'-f', project.genFullArgsFile.destination.get().asFile
            def dirPath = project.file("examples/$exampleName").path
            def files = new FileNameFinder().getFileNames dirPath,'*.sv'
            args files
            workingDir project.xrunDir
            project.mkdir workingDir
        }
    }
}
